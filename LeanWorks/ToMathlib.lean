/-
Copyright (c) 2025 Yannick Seurin. All rights reserved.
Released under MIT license as described in the file LICENSE.
Authors: Yannick Seurin
-/
import Mathlib.Tactic

/-!
General lemmas that could be ported to Mathlib, maybe...
-/

/-
This instance allows to infer `NeZero (Nat.card G)` when working with a `Fintype` group
See https://leanprover.zulipchat.com/#narrow/channel/113489-new-members/topic/instance.20synthesis.20failed
-/
instance {α : Type u} [Finite α] [Nonempty α] : NeZero (Nat.card α) where
  out := Nat.card_ne_zero.mpr ⟨inferInstance, inferInstance⟩

section Bijection

universe u v

/--
The n-fold product of a type.
-/
def nfoldProd (α : Type u) : ℕ → Type u
  | 0     => PUnit
  | 1     => α
  | n + 2 => α × nfoldProd α (n + 1)

/--
The application of a function `f` coordinate-wise on an n-tuple.
-/
def nfoldMap {α : Type u} {β : Type v} (f : α → β) : (n : ℕ) → nfoldProd α n → nfoldProd β n
  | 0, _       => PUnit.unit
  | 1, x       => f x
  | n + 2, (x, xs) => (f x, nfoldMap f (n + 1) xs)

variable {α : Type u} {β : Type v} {f : α → β}

theorem Function.bijective_iff_has_inverse' :
    Bijective f ↔ ∃ g : β → α, (∀ x : α, ∀ y : β, y = f x ↔ x = g y) := by
  constructor
  · intro f_bij
    rcases Function.bijective_iff_has_inverse.mp f_bij with ⟨g, gli, gri⟩
    use g
    intro x y
    constructor
    · intro h
      rw [eq_comm, h, gli]
    intro h
    rw [eq_comm, h, gri]
  rintro ⟨g, hg⟩
  apply Function.bijective_iff_has_inverse.mpr
  use g
  constructor
  · intro x
    have : f x = f x ↔ x = g (f x) := hg x (f x)
    tauto
  intro y
  have : g y = g y ↔ y = f (g y) := iff_comm.mp (hg (g y) y)
  tauto

theorem Function.bijective_nfold (n : ℕ) (hf : Function.Bijective f) :
    Function.Bijective (nfoldMap f n) := by
  apply (Function.bijective_iff_existsUnique (nfoldMap f n)).mpr
  cases n with
    | zero =>
      simp [nfoldProd, nfoldMap]
    | succ n =>
      induction n with
      | zero =>
        exact fun b ↦ Bijective.existsUnique hf b
      | succ n ih =>
        simp only [nfoldProd, nfoldMap, Prod.forall, Prod.mk.injEq] at *
        intro b bs
        specialize ih bs
        have hb : ∃! a, f a = b := by exact Function.Bijective.existsUnique hf b
        simp [ExistsUnique] at *
        rcases hb with ⟨a, ha, ha'⟩
        rcases ih with ⟨as, has, has'⟩
        use a, as
        tauto

end Bijection

section ENNReal

open ENNReal

theorem ENNReal.mul_natCast {a b : ℕ} : (a : ℝ≥0∞) * (b : ℝ≥0∞) = (↑(a * b) : ℝ≥0∞) := by
  exact Eq.symm (Nat.cast_mul a b)

theorem ENNReal.mul_inv_natCast {a b : ℕ} :
    ((a : ℝ≥0∞) * (b : ℝ≥0∞))⁻¹ = (a : ℝ≥0∞)⁻¹ * (b : ℝ≥0∞)⁻¹ := by
  apply ENNReal.mul_inv
  · right
    exact ENNReal.natCast_ne_top b
  left
  exact ENNReal.natCast_ne_top a

theorem ENNReal.inv_mul_cancel_natCast {a : ℕ} (ha : a ≠ 0) :
     (a : ℝ≥0∞)⁻¹ * (a : ℝ≥0∞) = 1 := by
  apply ENNReal.inv_mul_cancel
  · exact Nat.cast_ne_zero.mpr ha
  exact natCast_ne_top a

theorem ENNReal.mul_inv_cancel_natCast {a : ℕ} (ha : a ≠ 0) :
     (a : ℝ≥0∞) * (a : ℝ≥0∞)⁻¹  = 1 := by
  apply ENNReal.mul_inv_cancel
  · exact Nat.cast_ne_zero.mpr ha
  exact natCast_ne_top a

end ENNReal

section Group

instance {G : Type*} [Group G] [IsCyclic G] : CommGroup G := IsCyclic.commGroup

namespace Group

def IsGenerator (G : Type*) [Group G] (g : G) :=
  ∀ x : G, x ∈ Subgroup.zpowers g

/-- The subtype of generators of a group `G`.
We make it a subtype because, when drawing a random generator
`g` of `G`, we want to have access to a proof that it is a
generator (something we would not have with a Finset).
Note: if `g : generator G`, then `g` is actually a pair
`(g.val, g.property)` where `g.val : G` and `g.property` is a
proof that `g.val` is a generator of `G`. -/
def Generator (G : Type*) [Group G] :=
  { g : G // IsGenerator G g }

noncomputable instance (G : Type*) [Group G] [IsCyclic G] [Fintype G] :
    Fintype (Generator G) :=
  Subtype.fintype fun x ↦ ∀ (x_1 : G), x_1 ∈ Subgroup.zpowers x

instance (G : Type*) [Group G] [IsCyclic G] :
    Nonempty (Generator G) := by
  simp only [Generator, IsGenerator, nonempty_subtype]
  exact IsCyclic.exists_zpow_surjective

theorem g_order {G : Type*} [Group G] (g : G) (hg : IsGenerator G g) :
    orderOf g = Nat.card G :=
  orderOf_eq_card_of_forall_mem_zpowers hg

theorem zpow_val_add {G : Type*} [Group G] [Fintype G]
    (g : G) (hg : IsGenerator G g)
    (a b : ZMod (Nat.card G)) :
    g ^ (a + b).val = g ^ (a.val + b.val) := by
  apply pow_eq_pow_iff_modEq.mpr
  rw [g_order g hg]
  unfold Nat.ModEq
  have : (a + b).val % (Nat.card G) = (a + b).val :=
    Nat.mod_eq_of_lt (ZMod.val_lt (a + b))
  rw [this]
  exact ZMod.val_add a b

theorem zpow_val_mul {G : Type*} [Group G] [Fintype G]
    (g : G) (hg : IsGenerator G g)
    (a b : ZMod (Nat.card G)) :
    g ^ (a * b).val = g ^ (a.val * b.val) := by
  apply pow_eq_pow_iff_modEq.mpr
  rw [g_order g hg]
  unfold Nat.ModEq
  have : (a * b).val % (Nat.card G) = (a * b).val :=
    Nat.mod_eq_of_lt (ZMod.val_lt (a * b))
  rw [this]
  exact ZMod.val_mul a b

/--
In a cyclic group, exponentiation is bijective.
-/
theorem exp_bijective {G : Type*} [Group G] [Fintype G]
    (g : G) (hg : IsGenerator G g) :
    Function.Bijective fun (x : ZMod (Nat.card G)) ↦ g ^ x.val := by
  constructor
  · simp [Function.Injective]
    intro a₁ a₂ h
    rw [← ZMod.natCast_zmod_val a₁, ← ZMod.natCast_zmod_val a₂, ZMod.natCast_eq_natCast_iff]
    have : a₁.val ≡ a₂.val [MOD orderOf g] := pow_eq_pow_iff_modEq.mp h
    rw [g_order g hg] at this
    exact this
  simp [Function.Surjective]
  intro b
  rcases hg b with ⟨z, rfl⟩
  dsimp
  use (z : ZMod (Nat.card G))
  rw [← zpow_natCast g (z : ZMod (Nat.card G)).val, ZMod.val_intCast z]
  rw [← zpow_mod_orderOf g z, g_order g hg]

/--
In a cyclic group, exponentiation followed by multiplication by
a fixed group element is bijective.
-/
theorem exp_mul_bijective {G : Type*} [Group G] [Fintype G]
    (g m : G) (hg : IsGenerator G g) :
    Function.Bijective fun (x : ZMod (Nat.card G)) ↦ g ^ x.val * m := by
  change Function.Bijective ((fun (a : G) ↦ a * m) ∘ (fun (x : ZMod (Nat.card G)) ↦ g ^ x.val))
  apply Function.Bijective.comp
  · exact mulRight_bijective m
  exact exp_bijective g hg

end Group

end Group
